# Maintainer: Chris Watson <cawatson1993@gmail.com>

pkgname=windsurf
pkgver=1.7.3
pkgrel=2
pkgdesc="The new purpose-built IDE to harness magic"
arch=('x86_64')
url="https://windsurf.com/"
license=('custom') # should be fixed
depends=( # electron is added at build time. Traslative deps by electron could be removed
    'alsa-lib'
    'bash'
    'cairo'
    'dbus'
    'expat'
    'fontconfig'
    'gcc-libs'
    'gnupg'
    'gtk3'
    'libdrm'
    'libnotify'
    'libsecret'
    'libx11'
    'libxcb'
    'libxcomposite'
    'libxdamage'
    'libxext'
    'libxfixes'
    'libxkbcommon'
    'libxkbfile'
    'libxrandr'
    'libxss'
    'libxtst'
    'lsof'
    'mesa'
    'nspr'
    'nss'
    'shared-mime-info'
    'xdg-utils'
)
optdepends=('glib2: Needed for move to trash functionality'
            'org.freedesktop.secrets: Needed for settings sync'
            'libdbusmenu-glib: Needed for KDE global menu'
            'icu69: Needed for live share' #no longer needed?
            'vulkan-driver')
options=('!strip') # About 0.1MB strippable.
source=("https://windsurf-stable.codeiumdata.com/wVxQEIWkwPUEAGf3/apt/pool/main/w/windsurf/Windsurf-linux-x64-${pkgver}.deb"
		"https://gitlab.archlinux.org/archlinux/packaging/packages/code/-/raw/main/code.sh"
		'run.sh.in')
sha256sums=('0d1bf6aaf7145383d366d87ae4926a74b5ee6b025b2e0e159a098111806dbcc5'
            '5da1525b5fe804b9192c05e1cbf8d751d852e3717fb2787c7ffe98fd5d93e8c1'
            '32cd52025302ce8e5b0472cffca3c8df3b680c18fc3f36aa8f2c0cd3a0a18e31')
package() {
	bsdtar -xf "data.tar.xz" --exclude 'usr/share/windsurf/[^r]*' --exclude 'usr/share/windsurf/*.pak' \
		--exclude 'usr/share/pixmaps' --exclude 'usr/share/windsurf/resources/completions'
	# Fix path
	mv usr/share/{appdata,metainfo} #metainfo (is this needed)
 	mv usr/share/zsh/{vendor-completions,site-functions} #zsh
	# Get version of internal electron
	_elbin=electron$(grep -E '"electron": "[0-9]{2}' usr/share/windsurf/resources/app/package.json|awk '{print $2}'|cut -c2-3)
	depends+=($_elbin)
	# Launcher supports flag files
	sed -e s/name=electron/name=${_elbin}/ -e s/code-flags/windsurf-flags/ code.sh | head -n -2 | cat - run.sh.in > run.sh
	install -Dm755 run.sh usr/bin/windsurf
	ln -sf /usr/bin/windsurf usr/share/windsurf/windsurf 
	# Icon (1024^2 is unusable at KDE)
	install -Dm644 "usr/share/${pkgname}/resources/app/out/media/code-icon.svg" "usr/share/icons/hicolor/scalable/apps/${pkgname}.svg"
	mv usr "${pkgdir}"
}
