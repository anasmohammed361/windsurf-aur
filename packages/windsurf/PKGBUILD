# Maintainer: Chris Watson <cawatson1993@gmail.com>

pkgname=windsurf
pkgver=1.7.3
pkgrel=2
pkgdesc="The new purpose-built IDE to harness magic"
arch=('x86_64')
url="https://windsurf.com/"
license=('custom') # should be fixed
depends=( # electron is added at build time. Traslative deps by electron could be removed
    'alsa-lib'
    'bash'
    'cairo'
    'dbus'
    'expat'
    'fontconfig'
    'gcc-libs'
    'gnupg'
    'gtk3'
    'libdrm'
    'libnotify'
    'libsecret'
    'libx11'
    'libxcb'
    'libxcomposite'
    'libxdamage'
    'libxext'
    'libxfixes'
    'libxkbcommon'
    'libxkbfile'
    'libxrandr'
    'libxss'
    'libxtst'
    'lsof'
    'mesa'
    'nspr'
    'nss'
    'shared-mime-info'
    'xdg-utils'
)
optdepends=('glib2: Needed for move to trash functionality'
            'org.freedesktop.secrets: Needed for settings sync'
            'libdbusmenu-glib: Needed for KDE global menu'
            'icu69: Needed for live share' #no longer needed?
            'vulkan-driver')
options=('!strip') # About 0.1MB strippable.
source=("https://windsurf-stable.codeiumdata.com/wVxQEIWkwPUEAGf3/apt/pool/main/w/windsurf/Windsurf-linux-x64-${pkgver}.deb"
		"https://gitlab.archlinux.org/archlinux/packaging/packages/code/-/raw/main/code.sh"
		'run.sh.in')
sha256sums=('0d1bf6aaf7145383d366d87ae4926a74b5ee6b025b2e0e159a098111806dbcc5'
			'5da1525b5fe804b9192c05e1cbf8d751d852e3717fb2787c7ffe98fd5d93e8c1'
			'SKIP')
package() {
    bsdtar -xf "data.tar.xz"
    # Get version of internal electron
    _elbin=electron$(grep -E '"electron": "[0-9]{2}' usr/share/"${pkgname}"/resources/app/package.json|awk '{print $2}'|cut -c2-3)
    depends+=($_elbin)
    # Main resources
    install -d "${pkgdir}/usr/share/${pkgname}/resources/"
    mv "usr/share/${pkgname}/resources/app" "${pkgdir}/usr/share/${pkgname}/resources/app"
    # Launcher supports flag files
    sed -e s/name=electron/name=${_elbin}/ -e s/code-flags/windsurf-flags/ code.sh | head -n -2 | cat - run.sh.in > run.sh
    install -Dm755 run.sh "${pkgdir}/usr/bin/${pkgname}"
    ln -sf "/usr/bin/${pkgname}" "${pkgdir}/usr/share/${pkgname}/${pkgname}"
    # Desktop entries
    mv usr/share/applications "${pkgdir}/usr/share/applications"
    # Application assets
    install -Dm644 "usr/share/appdata/${pkgname}.appdata.xml" "${pkgdir}/usr/share/metainfo/com.codeium.${pkgname}.metainfo.xml"
    install -Dm644 "usr/share/mime/packages/${pkgname}-workspace.xml" "${pkgdir}/usr/share/mime/packages/${pkgname}-workspace.xml"
    # Scalable icon (build fails if locatio was changed)
    install -Dm644 "${pkgdir}/usr/share/${pkgname}/resources/app/out/media/code-icon.svg" "${pkgdir}/usr/share/icons/hicolor/scalable/apps/${pkgname}.svg"
    # Shell completions
    mv "usr/share/bash-completion" "${pkgdir}/usr/share/bash-completion"
    install -Dm644 "usr/share/zsh/vendor-completions/_${pkgname}" "${pkgdir}/usr/share/zsh/site-functions/_${pkgname}"
}
