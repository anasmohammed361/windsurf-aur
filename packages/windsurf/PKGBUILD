# Maintainer: Chris Watson <cawatson1993@gmail.com>

pkgname=windsurf
pkgver=1.7.3
pkgrel=2
pkgdesc="The new purpose-built IDE to harness magic"
arch=('x86_64')
url="https://windsurf.com/"
license=('custom') # should be fixed
_elnum=34
depends=( electron$_elnum ripgrep fd #replacements
    'alsa-lib'
    'cairo'
    'dbus'
    'expat'
    'gnupg'
    'libnotify'
    'libsecret'
    'libx11'
    'libxcb'
    'libxcomposite'
    'libxdamage'
    'libxext'
    'libxfixes'
    'libxkbcommon'
    'libxkbfile'
    'libxrandr'
    'libxss'
    'libxtst'
    'lsof'
    'mesa'
    'nspr'
    'shared-mime-info'
    'xdg-utils'
)
optdepends=('glib2: Move to trash functionality'
            'org.freedesktop.secrets: Sync settings'
            'libdbusmenu-glib: KDE global menu'
            'icu69: Live share' # obsolete?
            'vulkan-driver'
            'electron: /usr/share/windsurf/windsurf-latestron')
options=('!strip') # About 0.49MB strippable.
makedepends=('tar') # faster than bsdtar.
source=("https://windsurf-stable.codeiumdata.com/wVxQEIWkwPUEAGf3/apt/pool/main/w/windsurf/Windsurf-linux-x64-${pkgver}.deb"
		"https://gitlab.archlinux.org/archlinux/packaging/packages/code/-/raw/main/code.sh")
sha256sums=('0d1bf6aaf7145383d366d87ae4926a74b5ee6b025b2e0e159a098111806dbcc5'
            '5da1525b5fe804b9192c05e1cbf8d751d852e3717fb2787c7ffe98fd5d93e8c1')
package() {
	tar -xf "data.tar.xz" --exclude 'usr/share/windsurf/[^r]*' --exclude 'usr/share/windsurf/*.pak' \
		--exclude 'usr/share/pixmaps'
 	# Check version of electron
 	_correctron=$(grep -E '"electron": "[0-9]{2}' usr/share/windsurf/resources/app/package.json|awk '{print $2}'|cut -c2-3)
	if [[ $_elnum != $_correctron ]]; then
		echo "Using incorrectron. Change electron${_elnum} to electron${_correctron}"
		exit 1
	fi
	# Fix path
	mv usr/share/{appdata,metainfo} #metainfo (needed?)
 	mv usr/share/zsh/{vendor-completions,site-functions}
	# Launcheres
	_app=/usr/share/windsurf/resources/app
	sed -e "s|code-flags|windsurf-flags|" code.sh \
		-e "s|/usr/lib/code/out/cli.js|${_app}/out/cli.js|" \
		-e "s|/usr/lib/code/code.mjs|--app=${_app}|" > run.sh
	sed "s|name=electron|name=electron${_elnum}|" run.sh > run-safe.sh
	install -Dm755 run.sh usr/share/windsurf/windsurf-latestron
	install -Dm755 run-safe.sh usr/bin/windsurf
	ln -sf /usr/bin/windsurf usr/share/windsurf/windsurf
	# Replacements
	_bin=usr/share/windsurf/resources/app/extensions/windsurf/bin/fd
	rm ${_bin}.LICENSE # if fd was removed, detected as failure
	ln -sf /usr/bin/fd ${_bin}
	_bin=usr/share/windsurf/resources/app/node_modules/@vscode/ripgrep/bin/rg
	${_bin} -V > /dev/null # if rg was removed, detected as failure
	ln -sf /usr/bin/rg ${_bin}
	# Icon (1024^2 is unusable at KDE)
	install -Dm644 "usr/share/${pkgname}/resources/app/out/media/code-icon.svg" "usr/share/icons/hicolor/scalable/apps/${pkgname}.svg"
	mv usr "${pkgdir}"
}
