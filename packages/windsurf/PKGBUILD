# Maintainer: Chris Watson <cawatson1993@gmail.com>

pkgname=windsurf
pkgver=1.7.2
_elbin=electron34 #see package.json
pkgrel=3
pkgdesc="The new purpose-built IDE to harness magic"
arch=('x86_64')
url="https://windsurf.com/"
license=('custom')
depends=(
    'alsa-lib'
    'bash'
    'cairo'
    'dbus'
    'expat'
    ${_elbin}
    'fontconfig'
    'gcc-libs'
    'glibc>=2.28-4'
    'gnupg'
    'gtk3'
    'libdrm'
    'libnotify'
    'libsecret'
    'libx11'
    'libxcb'
    'libxcomposite'
    'libxdamage'
    'libxext'
    'libxfixes'
    'libxkbcommon'
    'libxkbfile'
    'libxrandr'
    'libxss'
    'libxtst'
    'lsof'
    'mesa'
    'nspr'
    'nss'
    'shared-mime-info'
    'xdg-utils'
)
optdepends=('glib2: Needed for move to trash functionality'
            'org.freedesktop.secrets: Needed for settings sync'
            'libdbusmenu-glib: Needed for KDE global menu'
            'icu69: Needed for live share')

options=('!strip') #done?

source=("https://windsurf-stable.codeiumdata.com/wVxQEIWkwPUEAGf3/apt/pool/main/w/windsurf/Windsurf-linux-x64-${pkgver}.deb")

sha256sums=('4742fe6a4591572faf75ec0668d0fedf22b3eeff6a9f344372d9edeede3057f5')

package() {
    bsdtar -xf "data.tar.xz"
    # Install main resources
    install -d "${pkgdir}/opt"
    mv "usr/share/${pkgname}/resources/app" "${pkgdir}/opt/${pkgname}-runtime"

    # Create launcher
    echo "#!/bin/sh" > run.sh
    echo "/usr/bin/${_elbin} /opt/${pkgname}-runtime \$@" >> run.sh
    install -Dm755 run.sh "${pkgdir}/usr/bin/${pkgname}"

    # Install desktop entries for application and URL handling
    mv usr/share/applications "${pkgdir}/usr/share/applications"
    # Install application metadata (AppStream metainfo)
    install -Dm644 "usr/share/appdata/${pkgname}.appdata.xml" "${pkgdir}/usr/share/metainfo/com.codeium.${pkgname}.metainfo.xml"
    install -Dm644 "usr/share/mime/packages/${pkgname}-workspace.xml" "${pkgdir}/usr/share/mime/packages/${pkgname}-workspace.xml"

    # KDE/GTK never look in 1024-px dirs; install dummy aliases too
    for _size in 128 256 1024; do
    	install -d "${pkgdir}/usr/share/icons/hicolor/${_size}x${_size}/apps"
    	ln -s "/opt/${pkgname}-runtime/resources/linux/code.png" "${pkgdir}/usr/share/icons/hicolor/${_size}x${_size}/apps/${pkgname}.png"
	done
    # Shell completions
    mv "usr/share/bash-completion" "${pkgdir}/usr/share/bash-completion"
    install -Dm644 "usr/share/zsh/vendor-completions/_${pkgname}" "${pkgdir}/usr/share/zsh/site-functions/_${pkgname}"
}
